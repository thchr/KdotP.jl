using LinearAlgebra: dot, cross, normalize

"""
    chern_2x2_hamiltonian(
        d; 
        cartesian::Bool = false,
        NŒ∏::Int = 31,
        NœÜ::Int = 51,
        k_abs::Real = 1e-1)      --> Float64

Evaluate the topological charge, or Chern number, of the lowest band of a 2√ó2 Hamiltonian
H(ùê§) = ùõî ‚ãÖ ùêù(ùê§), expanded on the Pauli matrices ùõî = [œÉ‚ÇÅ, œÉ‚ÇÇ, œÉ‚ÇÉ] and a coefficient vector
ùêù(ùê§) which completely determines Hamiltonian and is given as an input function `d`.
This function must return a 3D vector `[d‚ÇÅ, d‚ÇÇ, d‚ÇÉ]` for every input ùê§-vector.

The topological charge is evaluated over a ùê§-space sphere surrounding the origin ùê§ = ùüé.
The coefficient vector ùêù(ùê§) can be provided either in cartesian coordinates, i.e., as 
`d(k‚ÇÅ,k‚ÇÇ,k‚ÇÉ)`, or in spherical coordinates, i.e., as `d(Œ∏, œÜ, |k|)` (default); see keyword
argument `cartesian`.

## Method
The Chern number of the lowest band is evaluated by computing the total solid angle under
the manifold ÃÇùêù(S¬≤) with S¬≤ denoting the 2-sphere divided by 4œÄ, i.e., the degree of the map
ÃÇùêù. The Chern number of the lowest band is then C‚Çã = deg(ÃÇùêù)/4œÄ.
This is achieved numerically by discretization the 2-sphere on an equidistant grid in the
polar and azimuthal angles, and constructing a triangular mesh from each associated cell.

The Chern number of the highest band is the negated value of the first band's Chern number.

## Keyword arguments
- `cartesian :: Bool` (default, `false`): whether the input function `d` is provided in
  cartesian coordinates (`true`) or in spherical coordinates (`false`). If provided in
  cartesian coordinates, the function is internally converted to spherical coordinates.
- `NŒ∏ :: Integer` (default, `31`): number of discretization values of the polar angle.
- `NœÜ :: Integer` (default, `51`): number of discretization values of the azimuthal angle.
- `k_abs :: Real` (default, `1e-1`): ùê§-space radius of the sphere over which the Chern
  number is evaluated.

## Examples
We may e.g., compute the topological charge of the lowest band in a conventional Weyl
Hamiltonian H(ùê§) = ùõî ‚ãÖ ùê§:
```jldoctest
julia> d(Œ∏, œÜ, k) = [k*sin(Œ∏)*cos(œÜ), k*sin(Œ∏)*sin(œÜ), k*cos(Œ∏)]; # d(k‚ÇÅ,k‚ÇÇ,k‚ÇÉ) = [k‚ÇÅ,k‚ÇÇ,k‚ÇÉ]

julia> chern_2x2_hamiltonian(d)
1.000000000000007
```

If the function is only known in cartesian coordinates, it can be provided with the
`cartesian` keyword argument set to `true`:
```jldoctest
julia> d(k1, k2, k3) = [k1,k2,k3];

julia> chern_2x2_hamiltonian(d; cartesian=true)
1.000000000000007

julia> d(kx, ky, kz) = [kx^3-3kx*ky^2, 3kx^2*ky - ky^3, kz]; # Chern number of 3

julia> chern_2x2_hamiltonian(d; cartesian=true)
2.9999999999999756
```
"""
function chern_2x2_hamiltonian(
        d::Function;
        cartesian::Bool = false,
        NŒ∏::Int = 31,
        NœÜ::Int = 51,
        k_abs::Real = 1e-1)

    if cartesian
        d = _to_spherical_coordinates(d)
    end

    Œ∏s = range(0, stop=œÄ, length=NŒ∏) # discretize Œ∏ and œÜ
    œÜs = range(0, stop=2œÄ, length=NœÜ)
    
    Œ©_total = 0.0 # initialize total solid angle
    for i in 1:(NŒ∏-1) # over grid points in Œ∏ and œï
        Œ∏·µ¢, Œ∏·µ¢‚Çä‚ÇÅ = Œ∏s[i], Œ∏s[i+1]
        for j in 1:(NœÜ-1)
            œÜ‚±º, œÜ‚±º‚Çä‚ÇÅ = œÜs[j], œÜs[j+1]
            
            # values of d(Œ∏, œï) at at the four corners of the current grid cell
            d·µ¢‚±º     = d(Œ∏·µ¢,   œÜ‚±º, k_abs)
            d·µ¢‚Çä‚ÇÅ‚±º   = d(Œ∏·µ¢‚Çä‚ÇÅ, œÜ‚±º, k_abs)
            d·µ¢‚Çä‚ÇÅ‚±º‚Çä‚ÇÅ = d(Œ∏·µ¢‚Çä‚ÇÅ, œÜ‚±º‚Çä‚ÇÅ, k_abs)
            d·µ¢‚±º‚Çä‚ÇÅ   = d(Œ∏·µ¢,   œÜ‚±º‚Çä‚ÇÅ, k_abs)
            
            # solid angle for both triangles in the grid cell
            Œ©1 = triangle_solid_angle(d·µ¢‚±º, d·µ¢‚Çä‚ÇÅ‚±º‚Çä‚ÇÅ, d·µ¢‚±º‚Çä‚ÇÅ) # triangle 1
            Œ©2 = triangle_solid_angle(d·µ¢‚±º, d·µ¢‚Çä‚ÇÅ‚±º, d·µ¢‚Çä‚ÇÅ‚±º‚Çä‚ÇÅ) # triangle 2
            
           
            Œ©_total += Œ©1 + Œ©2 # accumulate total solid angle
        end
    end
    
    # Chern number is (minus) the degree of hat_d(k) over the unit sphere, divided by 4œÄ
    C = Œ©_total / (4œÄ) # minus sign is for the lowest band
    
    return C
end

# compute the solid angle subtended by a triangle with vertices d1, d2, d3
function triangle_solid_angle(
            d1::AbstractVector{<:Real},
            d2::AbstractVector{<:Real},
            d3::AbstractVector{<:Real}
            )
    d1 = normalize(d1) # must work with normalized d
    d2 = normalize(d2)
    d3 = normalize(d3)
   
    numerator = dot(d1, cross(d2, d3)) # scalar triple prod.: vol. parallelepiped {d1,d2,d3}
    denominator = 1 + dot(d1, d2) + dot(d3, d1) + dot(d2, d3)
    
    Œ© = 2 * atan(numerator, denominator) # solid angle of triangle projected to origin
    
    return Œ©
end

function _to_spherical_coordinates(d_cartesian::Function)
    # given a function in cartesian coordinates (kx, ky, kz), `d_cartesian`, return a
    # function that takes input in spherical coordinates (Œ∏, œÜ, |k|) instead
    return function(Œ∏::Real, œÜ::Real, k_abs::Real)
        sinŒ∏, cosŒ∏ = sincos(Œ∏)
        sinœÜ, cosœÜ = sincos(œÜ)
        kx = k_abs * sinŒ∏ * cosœÜ
        ky = k_abs * sinŒ∏ * sinœÜ
        kz = k_abs * cosŒ∏
        return d_cartesian(kx, ky, kz)
    end
end
